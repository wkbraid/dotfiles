#!/usr/bin/perl

use strict;
use warnings;

use File::HomeDir;
use File::Basename;
use File::Copy qw(move);

my $home = File::HomeDir->my_home;
my $dotpath = "$home/.dotfiles";

my $commands = { # {{{
    'help' => {
        'desc' => 'Display this help information.',
        'usage' => 'Usage: dotfiles help [command]',
        'func' => \&help,
    },
    'add' => {
        'desc' => 'Begin tracking a file or directory.',
        'usage' => 'Usage: dotfiles add [home | bin | config] <file>',
        'func' => \&add,
    },
    'restore' => {
        'desc' => 'Stop tracking a file and restore it to the home directory.',
        'usage' => 'Usage: dotfiles restore [home | bin | config] <file>',
        'func' => \&restore,
    },
    'list' => {
        'desc' => 'List all of the tracked files and directories.',
        'usage' => 'Usage: dotfiles list',
        'func' => \&list,
    },
    'sync' => {
        'desc' => 'Pull and sync remote changes to the home directory.',
        'usage' => 'Usage: dotfiles sync',
        'func' => \&sync,
    },
    'install' => {
        'desc' => 'Run installation scripts, can be used with the --all option.',
        'usage' => 'Usage: dotfiles install [--all | <install-script>]',
        'func' => \&install,
    },
    'commit' => {
        'desc' => 'Commit changes to the underlying repository.',
        'usage' => 'Usage: dotfiles commit <message>',
        'func' => \&commit,
    },
    'pull' => {
        'desc' => 'Perform a git pull on the underlying repository.',
        'usage' => 'Usage: dotfiles pull',
        'func' => \&pull,
    },
    'push' => {
        'desc' => 'Performs a git push on the underlying repository.',
        'usage' => 'Usage: dotfiles push',
        'func' => \&push,
    },
    'git' => {
        'desc' => 'Shortcut to run arbitrary git commands on the underlying repository.',
        'usage' => 'Usage: dotfiles git [<args>...]',
        'func' => \&git,
    },
}; # }}}

# The folders we might want to sync
my $contexts = { # {{{
    'home' => {
        'from' => "$home",
        'to' => "$dotpath/home",
    },
    'bin' => {
        'from' => "$home/bin",
        'to' => "$dotpath/bin",
    },
    'config' => {
        'from' => "$home/.config",
        'to' => "$dotpath/config",
    },
}; # }}}

sub main {
    my $args = scalar(@_);
    if ($args == 0) { help(); return }

    my $cmd = shift;
    $commands->{$cmd}{func}(@_);
    # TODO check if command exists before calling
}

sub query {
    my $question = shift;
    print "$question [Y/n] ";
    while() {
        my $response = <STDIN>;
        chomp $response;
        if ($response eq '' or $response eq 'y' or $response eq 'Y') {
            return 1;
        } elsif ($response eq 'n' or $response eq 'N') {
            return 0;
        }
        print "Please enter y or n: "; 
    }
}

sub help { # {{{
    my $args = scalar(@_);
    if ($args == 0) {
        print "Usage: dotfiles <command> [<arguments>]\n\n";

        print "Options:\n",
              "  -h --help      Display this help information.\n\n";

        print "Commands:\n";
        foreach my $cmd (sort keys %$commands) {
            printf "  %-20s%s\n", $cmd, $commands->{$cmd}{desc};
        }

        print "\nFor more information, see the README.\n";
    } elsif ($args == 1) {
        my $cmd = shift;
        printf "%s\n", $commands->{$cmd}{usage};
    } else {
        help('help');
    }
} # }}}

#= Dotfiles {{{ ===============================================================
sub add {
    if (scalar(@_) == 0) { return help('add') }

    my $context = 'home';
    my $file = shift;
    foreach my $ctx (sort keys %$contexts) {
        if ($file eq $ctx) {
            $context = $ctx;
            if (scalar(@_) != 1) { return help('add') }
            $file = shift;
            last;
        }
    }

    $file = basename($file);
    my $original = "$contexts->{$context}{from}/$file";
    my $tracked = "$contexts->{$context}{to}/$file";

    if (-l $original) { die "Symlink already exists.\n"; }
    if ((not -f $original) and (not -d $original)) {
        die "Cannot find file or directory: $original\n"; }
    if ($original eq '/' or $original eq '..' or $original eq '.') {
        die "Invalid file name $original\n"; }
    
    if (-e $tracked) {
        print "Overwriting existing file $tracked\n";
        if (query()) {
            unlink $tracked or die "Unable to remove $tracked.\n";
        } else {
            die "Exiting...\n"; 
        }
    }

    move $original, $tracked or die "Unable to move $original to $tracked.\n";
    print "Linking $original => $tracked...\n";
    symlink $tracked, $original or die "Unable to link $original => $tracked.\n";
}

sub restore {
    if (scalar(@_) == 0) { help('restore'); return; }
    
    my $context = 'home';
    my $file = shift; 
    foreach my $ctx (sort keys %$contexts) {
        if ($file eq $ctx) {
            $context = $ctx;
            if (scalar(@_) != 1) { return help('restore') }
            $file = shift;
            last;
        }
    }

    $file = basename($file);
    my $original = "$contexts->{$context}{from}/$file";
    my $tracked = "$contexts->{$context}{to}/$file";

    if (not -e $tracked) {
        die "Could not find file or directory: $tracked.\n"; }
    if (not -l $original) {
        die "The file $original is not a symlink.\n"; }
    
    print "Replacing linked $original with actual file $tracked...\n";
    unlink $original or die "Unable to remove $original.\n";
    move $tracked, $original or die "Unable to move $tracked to $original.\n";
    
}

sub list {
    foreach my $ctx (sort keys %$contexts) {
        my $dir = $contexts->{$ctx}{to};
        opendir(my $dh, "$dir") or die "Unable to open $dir.\n";
        my @files = readdir($dh);
        closedir($dh);
        foreach my $file (@files) {
            next if ($file =~ /^\.+$/);
            my $tracked = "$dir/$file";
            my $original = "$contexts->{$ctx}{from}/$file";
            printLink($tracked, $original);

        }
    }
}

sub printLink {
    my $tracked = shift;
    my $original = shift;
    if (not -e $original) {
        $original = "[missing]";
    } elsif (not -l $original) {
        $original = "[not a symbolic link]";
    }

    printf "%-49s <= $original\n", $tracked;
}

# }}}

#= Configuration {{{ ==========================================================
sub sync {
    print "Checking for remote changes...\n";
    pull();

    foreach my $ctx (sort keys %$contexts) {
        my $dir = $contexts->{$ctx}{to};
        opendir(my $dh, "$dir") or die "Unable to open $dir.\n";
        my @files = readdir($dh);
        closedir($dh);
        foreach my $file (@files) {
            next if ($file =~ /^\.+$/);
            my $tracked = "$dir/$file";
            my $original = "$contexts->{$ctx}{from}/$file";
            syncFile($tracked, $original);
        }

    }
}

sub syncFile {
    my $tracked = shift;
    my $original = shift;

    print "Attempting to sync $tracked...\n";
    if (-l $original) {
        print "Link already exists at $original.\n"; return;
    } 
    if (-e $original) {
        if (query("File $original already exists. Overwrite?")) {
            print "Removing file $original...\n";
            unlink $original or die "Unable to remove $original.\n";
        }
    }

    print "Linking $original => $tracked...\n";
    symlink $tracked, $original or die "Unable to link $original => $tracked.\n"; 
}

sub install {
    if (scalar(@_) != 1) { return help('install'); }
    my $script = shift;
    if ($script eq '--all') {
        opendir(my $DH, "$dotpath/install/") or die "Unable to open $dotpath/install/.\n";
        my @files = readdir($DH);
        closedir($DH);
        foreach my $file (@files) {
            if (-f $file) {
                install(basename($file)) 
            }
        }
    } else {
        my $file = "$dotpath/scripts/$script";
        (-e $file) or die "File $file does not exist.\n";
        print "Executing $file...\n";
        system($file) or die "Error while executing $file.\n";
    }
}
# }}}

#= Git {{{ ====================================================================
sub commit {
    if (scalar(@_) == 1) {
        my $msg = shift;
        system("git", "-C", "$dotpath/", "add", ".");
        system("git", "-C", "$dotpath/", "commit", "-am", $msg);
    } else {
        help('commit');
    }
}

sub pull {
    my $args = scalar(@_);
    if ($args == 0) {
        system("git", "-C", "$dotpath/", "pull");
    } else {
        help('pull');
    }
}

sub push {
    my $args = scalar(@_);
    if ($args == 0) {
        system("git", "-C", "$dotpath/", "push");
    } else {
        help('push');
    }
}

sub git {
    system("git", "-C", "$dotpath/", @_);
}
# }}}

main(@ARGV);
